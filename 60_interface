# -*- Mode: Shell-script; -*-

# .zsh/interface : Mark Tran <mark@nirv.net>

# prompt
PS1='%m(%35<...<%~) [%!] %(?..!%?! )%# '

function title {
    local value="${${${(V)1//\%/\%\%}//'\n'/; }//'\t'/ }"
    value="%70>...>$value%<<"

    case $TERM in
        xterm*|rxvt*)
            if [[ -z $SSH_TTY ]]; then
                print -Pn "\e]0;$value $2\a"
            else
                print -Pn "\e]0;$value@$HOST $2\a"
            fi
            ;;
    esac
}

# chpwd is called when the current working directory is changed
function chpwd {
    ls
}

# precmd is called just before the prompt is printed
function precmd {
    title "zsh" "%~"
}

# preexec is called just before any command line is executed
function preexec {
    title "$1"
}
